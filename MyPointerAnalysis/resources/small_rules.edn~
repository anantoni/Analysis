[
[ (MethodDeclaration ?signature ?method)
  [?methodDeclaration :MethodDeclaration/signature ?signature]
  [?methodDeclaration :MethodDeclaration/ref ?method] ]

[ (Reachable ?method)
  [?mainMethodDeclaration :MainMethodDeclaration/method ?method] ]

[ (Reachable ?method) 
  [?implicitReachable :ImplicitReachable/sig ?method] ]

[ (Reachable ?tomethod)
  (CallGraphEdge _ ?tomethod) ]

[ (CallGraphEdge ?invocation ?tomethod)
  (Reachable ?inmethod)
  [?virtualMethodInvocation :VirtualMethodInvocation/inmethod ?inmethod]  
  [?virtualMethodInvocation :VirtualMethodInvocation/invocation ?invocation]
  [?virtualMethodInvocation :VirtualMethodInvocation/signature ?signature]
  [?virtualMethodInvocationBase :VirtualMethodInvocation-Base/invocation ?invocation]
  [?virtualMethodInvocationBase :VirtualMethodInvocation-Base/base ?base] 
  [?methodSignatureSimplename :MethodSignature-SimpleName/signature ?signature]
  [?methodSignatureSimplename :MethodSignature-SimpleName/simplename ?simplename]
  [?methodSignatureDescriptor :MethodSignature-Descriptor/signature ?signature]
  [?methodSignatureDescriptor :MethodSignature-Descriptor/descriptor ?descriptor]
  (VarPointsTo ?heap ?base)
  (HeapAllocationType ?heap ?type)
  [?methodLookup :MethodLookup/simplename ?simplename]
  [?methodLookup :MethodLookup/descriptor ?descriptor]
  [?methodLookup :MethodLookup/type ?type]
  [?methodLookup :MethodLookup/method ?tomethod] ]
	

[ (Reachable ?clinit)
  [?initializedClass :InitializedClass/classOrInterface ?class]
  [?classInitializer :ClassInitializer/type ?class]
  [?classInitializer :ClassInitializer/method ?clinit] ] 

[ (CallGraphEdge ?invocation ?signature)
  (Reachable ?inmethod)
  [?staticMethodInvocation :StaticMethodInvocation/inmethod ?inmethod]
  [?staticMethodInvocation :StaticMethodInvocation/invocation ?invocation]
  [?staticMethodInvocation :StaticMethodInvocation/signature ?signature]   
  ]	
    
[ (VarPointsTo ?heap ?var)
  (Reachable ?inmethod)
  [?assignHeapNormalAllocation :AssignNormalHeapAllocation/inmethod ?inmethod]
  [?assignHeapNormalAllocation :AssignNormalHeapAllocation/heap ?heap]
  [?assignHeapNormalAllocation :AssignNormalHeapAllocation/var ?var] ]

[ (VarPointsTo ?heap ?var)
  (Reachable ?inmethod)
  [?assignAuxiliaryHeapAllocation :AssignAuxiliaryHeapAllocation/inmethod ?inmethod]
  [?assignAuxiliaryHeapAllocation :AssignAuxiliaryHeapAllocation/heap ?heap]
  [?assignAuxiliaryHeapAllocation :AssignAuxiliaryHeapAllocation/var ?var] ]

[ (VarPointsTo ?heap ?to)
  (Reachable ?inmethod)
  [?assignLocal :AssignLocal/inmethod ?inmethod]
  [?assignLocal :AssignLocal/to ?to]
  [?assignLocal :AssignLocal/from ?from]
  (VarPointsTo ?heap ?from) ]

]